library PingPong

let one_msg = 
  fun (msg : Message) => 
    let nil_msg = Nil {Message} in
      Cons {Message} msg nil_msg

let game_over = Int32 0
let calling_pong = Int32 1
let addr_not_set = Int32 2

(***************************************************)
(*             The contract definition             *)
(***************************************************)
contract Ping

(* No immutable parameters *)
()

(* Mutable fields *)
field count : Int32 = Int32 2
field pong_addr : Option ByStr20 = None {ByStr20}

transition Ping ()
  one = Int32 1;
  paOpt <- pong_addr;
  match paOpt with
  | Some pongAddr =>
    cnt <- count;
    is_game_over = builtin lt cnt one;
    match is_game_over with
    | True =>
      msg = {_tag : Main; _recipient : _sender; _amount : Uint128 0; code : game_over};
      msgs = one_msg msg;
      send msgs
    | False =>
      deccount = builtin sub cnt one;
      count := deccount;
      msg = {_tag : Pong; _recipient : pongAddr; _amount : Uint128 0; code : calling_pong};
      msgs = one_msg msg;
      send msgs
    end
  | None =>
    msg = {_tag : Main; _recipient : _sender; _amount : Uint128 0; code : addr_not_set};
    msgs = one_msg msg;
    send msgs
  end
end

transition SetPongAddr (pongAddr : ByStr20)
  paOpt = Some {ByStr20} pongAddr;
  pong_addr := paOpt
end
